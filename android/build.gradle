import groovy.json.JsonSlurper

def getPackageVersion() {
    return new JsonSlurper().parse(file('../package.json'), 'utf-8').version
}

def safeExtGet(prop, fallback) {
    rootProject.hasProperty(prop) ? rootProject.ext.get(prop) : fallback
}

def DEF_BUILD_TOOLS_VERSION = '26.0.3'
def DEF_COMPILE_SDK_VERSION = 26
def DEF_TARGET_SDK_VERSION  = 26
def DEF_MIN_SDK_VERSION     = 16

buildscript {
    repositories {
        maven {
            url 'https://maven.google.com/'
            name 'google'
        }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEF_COMPILE_SDK_VERSION)
    buildToolsVersion safeExtGet('buildToolsVersion', DEF_BUILD_TOOLS_VERSION)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', DEF_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet('targetSdkVersion', DEF_TARGET_SDK_VERSION)
        versionCode 2
        versionName getPackageVersion()
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
        name 'React Native (local)'
    }
    maven {
        url 'https://maven.google.com/'
        name 'google'
    }
    jcenter()
}

dependencies {
    compile 'com.facebook.react:react-native:+'
}
